plugins {
    id 'java'
    id 'application'
    id 'jacoco'
}

application {
    mainClass = 'org.httpserver.App'
}

group 'org.httpserver'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testImplementation 'org.mockito:mockito-core:4.6.1'
    implementation 'ch.qos.logback:logback-core:1.2.11'
    implementation 'ch.qos.logback:logback-classic:1.2.11'
    implementation 'org.slf4j:slf4j-api:1.7.36'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.3'
}


jar {
    manifest {
        attributes 'Main-Class': 'org.httpserver.App'
    }
}

task stage {
    dependsOn build
}
build.mustRunAfter clean

gradle.taskGraph.whenReady {
    taskGraph ->
        if (taskGraph.hasTask(stage)) {
            test.enabled = false
        }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
//    finalizedBy jacocoTestReport
}

jacoco {
    applyTo run
    toolVersion = "0.8.7"
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.5
            }
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled false
        html.enabled true
        csv.enabled false
    }
}
//check.dependsOn jacocoTestReport
